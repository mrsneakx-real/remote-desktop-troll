plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

javafx {
    version = "21.0.2"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

application {
    mainClass = 'main.AppMain'
    applicationDefaultJvmArgs = ["--enable-native-access=javafx.graphics"]
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': application.mainClass.get()
        )
    }
}

tasks.register('fatJar', Jar) {
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'Main-Class': application.mainClass.get()
        )
    }

    // Zugriff auf SourceSets Ã¼ber Extensions (Gradle 9 kompatibel)
    def sourceSets = project.extensions.getByType(SourceSetContainer)
    from(sourceSets.named("main").get().output)

    // Dependencies einpacken
    from({
        configurations.runtimeClasspath
                .filter { it.name.endsWith("jar") }
                .collect { zipTree(it) }
    })
}